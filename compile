#!/bin/csh -f

if ($#argv == 1) then
  set MACHINE=$1
else if ($#argv == 0) then
  echo "Warning: assuming on jet machine"
  set MACHINE=jet
else
  echo "Usage: $0 machine"
  echo "Install compile WW3 on this machine"
  echo "Example: $0 jet"
  exit 1
endif

if ( ! -d build ) then
  echo "Warning: Need to run ./configure first"
  echo "Exiting: do nothing"
  exit 1
endif

echo ${MACHINE}

# check NETCDF environment variables
if ( $MACHINE == jet ) then
  if ( $?NETCDF4 ) then
    setenv WWATCH3_NETCDF NC4
    setenv NETCDF_CONFIG ${NETCDF4}/bin/nc-config
  else if ( $?NETCDF ) then
    setenv WWATCH3_NETCDF NC3
    setenv NETCDF_LIBDIR  ${NETCDF}/lib
    setenv NETCDF_INCDIR  ${NETCDF}/include
    echo ${WWATCH3_NETCDF}
    echo ${NETCDF_INCDIR}
    echo ${NETCDF_LIBDIR}
  else
    echo "WARNING: WWATCH3_NETCDF not defined"
  endif
else if ( $MACHINE == wcosscray ) then
    setenv WWATCH3_NETCDF NC4
# Note:
# This nc-config currently is not the correct one. 
# The comp/link options for NETCDF is actually manually specified by comp/link.Intel_wcosscray.
    set NC_CONFIG=`which nc-config` 
    setenv NETCDF_CONFIG ${NC_CONFIG}
    echo ${WWATCH3_NETCDF}
    echo ${NETCDF_CONFIG}
else if ( $MACHINE == wcoss ) then
    setenv WWATCH3_NETCDF NC4
    setenv NETCDF_CONFIG ${NETCDF}/bin/nc-config
    echo ${WWATCH3_NETCDF}
    echo ${NETCDF_CONFIG}
else if ( $MACHINE == theia ) then
    setenv WWATCH3_NETCDF NC4
    setenv NETCDF_CONFIG ${NETCDF}/bin/nc-config
    echo ${WWATCH3_NETCDF}
    echo ${NETCDF_CONFIG}
else
    echo "WARNING: Unkown machine"
    echo "WARNING: WWATCH3_NETCDF not defined"
endif

cd build/

if ( ! -d bin ) then

# Link install script and run install 
ln -sf svn/model/bin/install_ww3_svn .

# Install WW3 into the build directory
./install_ww3_svn << EOF
y
L
y
n
y
n
EOF

endif

cd bin

# choose correct comp and link files
# can be done in the configure step
cp comp.Intel_$MACHINE comp
cp link.Intel_$MACHINE link

# use proper switch file
cp switch_CPL_HWRF switch
#cp switch_NCEP_st4 switch
#cp switch_NCEP_st2 switch

# compile WW3 
./make_CPL

echo 'Compile WW3 done'

